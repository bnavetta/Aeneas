import de.gesellix.gradle.docker.tasks.*

plugins {
	id 'java'
	id 'us.kirchmeier.capsule' version '0.10.0'
	id 'io.spring.dependency-management' version '0.3.0.RELEASE'
	id 'de.gesellix.docker' version '2014-12-27T20-16-13'
}

dependencies {
	compile('org.apache.zookeeper:zookeeper') {
		exclude group: 'org.slf4j', module: 'slf4j-log4j12'
		exclude module: 'log4j'
	}

	compile 'org.mousio:etcd4j'
	compile 'io.netty:netty-codec-http' // because of how etcd4j sets up dependencies

	compile 'com.google.guava:guava'

	compile 'org.slf4j:slf4j-simple'
	compile 'org.slf4j:slf4j-api'
}

task capsule(type: FatCapsule) {
	applicationClass 'com.bennavetta.aeneas.Main'
}

task dockerImage(type: DockerBuildTask) {
	dependsOn capsule
	imageName = 'zookeeper-configurator'
	buildContextDirectory = projectDir
}

build.dependsOn dockerImage
